#include "RX5808.h"

// Initialise RX5808 receiver
RX5808::RX5808(uint8_t data, uint8_t le, uint8_t clk, uint8_t rssi, Settings *s)
  : rssiValues(0), lowband(false),
    dataPin(data), lePin(le), clkPin(clk), rssiPin(rssi),
    scanHandle(NULL), stopRequested(false), settings(s) {

  // Setup spi pins
  pinMode(dataPin, OUTPUT);
  pinMode(lePin, OUTPUT);
  pinMode(clkPin, OUTPUT);

  // Setup rssi pin
  pinMode(rssiPin, INPUT);

  // Set inital pin state
  digitalWrite(lePin, HIGH);
  digitalWrite(clkPin, LOW);

  // Initialise markers to invalid state
  for (int i = 0; i < MAX_MARKERS; i++) {
    markers[i].index = -1;
    markers[i].rssi = 0;
  }

  // Create mutexes
  scanMutex = xSemaphoreCreateMutex();
  lowbandMutex = xSemaphoreCreateMutex();
  markersMutex = xSemaphoreCreateMutex();

  // Reset receiver
  reset();
}

// Start background scanning
void RX5808::startScan() {
  // Start scanning task only if not already running
  if (scanHandle == NULL) {
    xTaskCreate(_scan, "scan", SCAN_STACK_SIZE, this, 1, &scanHandle);
  }
}

// Stop background scanning
void RX5808::stopScan() {
  // Cancel scanning task only if already running
  if (scanHandle != NULL) {
    stopRequested = true;
  }
}

// Save current rssi as high/low calibration
void RX5808::calibrate(bool high) {
  // Set to F4
  setFrequency(5800);

  // Give extended time for rssi to stabilise accurately during calibration
  delay(50);

  // Save rssi
  if (high) {
    xSemaphoreTake(settings->settingsMutex, portMAX_DELAY);
    settings->highCalibratedRssi.set(readRSSI());
    xSemaphoreGive(settings->settingsMutex);
  } else {
    xSemaphoreTake(settings->settingsMutex, portMAX_DELAY);
    settings->lowCalibratedRssi.set(readRSSI());
    xSemaphoreGive(settings->settingsMutex);
  }
}

// Background task that runs scanning continuously
void RX5808::_scan(void *parameter) {
  // Static cast weirdness to access parameters
  RX5808 *receiver = static_cast<RX5808 *>(parameter);

  // Get interval at which to scan
  xSemaphoreTake(receiver->settings->settingsMutex, portMAX_DELAY);
  float interval = receiver->settings->scanInterval.get();
  xSemaphoreGive(receiver->settings->settingsMutex);

  // Calculate number of values to scan
  int numScannedValues = (SCAN_FREQUENCY_RANGE / interval) + 1;  // +1 for final number inclusion

  // Loop continuously
  // Stops when scanning task cancelled
  while (!receiver->stopRequested) {
    for (int i = 0; i < numScannedValues; i++) {
      // Safely stop scanning when no mutexes taken
      if (receiver->stopRequested) break;

      // Read lowband state per frequency to respond immediately to band changes
      xSemaphoreTake(receiver->lowbandMutex, portMAX_DELAY);
      bool lowband = receiver->lowband.get();
      xSemaphoreGive(receiver->lowbandMutex);

      int min_freq = lowband ? LOWBAND_MIN_FREQUENCY : HIGHBAND_MIN_FREQUENCY;

      // Set frequency and offset by minimum
      receiver->setFrequency((int)round(i * interval + min_freq));

      // Give time for rssi to stabilise
      vTaskDelay(pdMS_TO_TICKS(RSSI_STABILISATION_TIME));

      // Safely stop scanning when no mutexes taken
      // Second call in case task cancelled during delay
      if (receiver->stopRequested) break;

      // Take mutex to safely modify data in this task
      xSemaphoreTake(receiver->scanMutex, portMAX_DELAY);
      receiver->rssiValues.set(i, receiver->readRSSI());
      xSemaphoreGive(receiver->scanMutex);
    }

    // After each full pass, compute peak markers if requested
    if (!receiver->stopRequested) {
      xSemaphoreTake(receiver->settings->settingsMutex, portMAX_DELAY);
      int numMarkers = receiver->settings->markerCount.get();
      xSemaphoreGive(receiver->settings->settingsMutex);

      if (numMarkers > 0) {
        bool used[MAX_FREQUENCIES_SCANNED];
        memset(used, 0, sizeof(used));
        MarkerData newMarkers[MAX_MARKERS];
        for (int m = 0; m < MAX_MARKERS; m++) {
          newMarkers[m].index = -1;
          newMarkers[m].rssi = 0;
        }

        xSemaphoreTake(receiver->scanMutex, portMAX_DELAY);
        for (int m = 0; m < numMarkers; m++) {
          int maxVal = -1, maxIdx = -1;
          for (int i = 0; i < numScannedValues; i++) {
            if (!used[i] && receiver->rssiValues.get(i) > maxVal) {
              maxVal = receiver->rssiValues.get(i);
              maxIdx = i;
            }
          }
          if (maxIdx >= 0) used[maxIdx] = true;
          newMarkers[m].index = maxIdx;
          newMarkers[m].rssi = maxVal;
        }
        xSemaphoreGive(receiver->scanMutex);

        xSemaphoreTake(receiver->markersMutex, portMAX_DELAY);
        for (int m = 0; m < MAX_MARKERS; m++) {
          receiver->markers[m] = (m < numMarkers) ? newMarkers[m] : MarkerData{-1, 0};
        }
        xSemaphoreGive(receiver->markersMutex);
      }
    }
  }

  // Task closed
  receiver->scanHandle = NULL;
  receiver->stopRequested = false;
  vTaskDelete(NULL);
}

// Set receiver frequency
void RX5808::setFrequency(int frequency) {
  // Calculate frequency value to send to receiver
  unsigned long toSend = frequencyToRegister(frequency);

  // Send data to 0x1 register
  sendRegister(0x01, toSend);
}

// Read rssi from receiver
int RX5808::readRSSI() {
  // Record multiple rssi values and average
  int rssi = 0;
  for (int i = 0; i < RSSI_SAMPLES; i++) {
    rssi += analogRead(rssiPin);
  }
  rssi /= RSSI_SAMPLES;

  return rssi;
}

// Reset receiver
void RX5808::reset() {
  sendRegister(0x0F, 0b00000000000000000000);
}

// Send data to specified receiver register
void RX5808::sendRegister(byte address, unsigned long data) {
  // Begin transmission
  digitalWrite(lePin, LOW);

  // Send address (LSB)
  for (int i = 0; i < 4; i++) {
    sendBit(bitRead(address, i));
  }

  // Set to write mode
  sendBit(1);

  // Send data bits (LSB)
  for (int i = 0; i < 20; i++) {
    sendBit(bitRead(data, i));
  }

  // End transmission
  digitalWrite(lePin, HIGH);
}

// Send 0 or 1 to receiver
void RX5808::sendBit(bool bit) {
  // Set data value
  digitalWrite(dataPin, bit);

  // Pulse clock
  digitalWrite(clkPin, HIGH);
  delayMicroseconds(10);
  digitalWrite(clkPin, LOW);
}

// Convert frequency number to required binary representation
unsigned long RX5808::frequencyToRegister(int frequency) {
  // Calculate parts to send to receiver
  frequency -= 479;
  int n = frequency / 64;
  int a = (frequency % 64) / 2;

  // Calculate frequency value to send to receiver
  return (n << 7) | a;
}
